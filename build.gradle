buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
		maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }

    dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'

version = '1.10.3'
group = 'bl4ckscor3.mod.globalxp'

base {
	archivesName = 'globalxp-1.20'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.20'
	copyIdeResources = true
	
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'

            mods {
                globalxp {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
			args '-nogui'

            mods {
                globalxp {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
    	url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.20-46.0.12'

    implementation fg.deobf("curse.maven:jade-324717:4573193")
    implementation fg.deobf("curse.maven:the-one-probe-245211:4579432")
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "Global XP",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Global XP",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}