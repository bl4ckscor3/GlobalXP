plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.gradle.userdev' version '[7.0,7.1)'
//	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.10.4'
group = 'bl4ckscor3.mod.globalxp'

base {
	archivesName = 'globalxp-neoforge-1.20.2'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run/client')
    }

    server {
        workingDirectory project.file('run/server')
		programArgument '-nogui'
    }
}

repositories {
    maven {
    	url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	implementation 'net.neoforged:neoforge:20.2.47-beta'

    implementation "curse.maven:jade-324717:4845321"
	implementation "curse.maven:the-one-probe-245211:4629624"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "Global XP",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Global XP",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}